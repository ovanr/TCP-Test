{-
TorXakis - TCP Model Based Testing
-}

-- ----------------------------------------------------------------------------------------- --

TYPEDEF TCPFlag ::= SYN | ACK | FIN | RST ENDDEF
TYPEDEF SEQ ::= SEQ_VALID | SEQ_INVALID ENDDEF
TYPEDEF ACK ::= ACK_VALID | ACK_INVALID ENDDEF

-- ascending alphabetical order is implied
TYPEDEF TCPFlagList ::=   
    NIL
    | CONS  { hd :: TCPFlag ; tl :: TCPFlagList }
ENDDEF

TYPEDEF TCPUserCallInput ::= 
    LISTEN { lport :: Int }
    | CONNECT { cport :: Int }
    | SEND { spayload :: String }
    | RECEIVE 
    | CLOSE
ENDDEF
TYPEDEF TCPUserCallOutput ::= 
    SUCCESS
    | FAILURE
    | RECEIVE { rpayload :: String }
ENDDEF

TYPEDEF TCPPacket ::= TCPPacket { 
        sport :: Int;
        dport :: Int;
        seq :: SEQ;
        ack :: ACK;
        flags :: TCPFlagList;
        payload :: String
    } 
ENDDEF

-- ----------------------------------------------------------------------------------------- --

CHANDEF  Channels  ::=    InSutNet   :: TCPPacket; 
                          OutSutNet   :: TCPPacket; 
                          InSutUser   :: TCPUserCallInput; 
                          OutSutUser   :: TCPUserCallOutput
ENDDEF

-- ----------------------------------------------------------------------------------------- --

FUNCDEF isValidString ( x :: String ) :: Bool ::=
    strinre (x, REGEX('[A-Za-z]{0,50}')) 
ENDDEF

FUNCDEF isListenUserCall ( c :: TCPUserCallInput ) :: Bool ::=
    IF isLISTEN(c) THEN
        (lport(c) > 4002) /\ (lport(c) < 5000)
    ELSE
        False
    FI
ENDDEF

FUNCDEF isConnectUserCall ( c :: TCPUserCallInput ) :: Bool ::=
    IF isCONNECT(c) THEN
        (cport(c) > 4002) /\ (cport(c) < 5000)
    ELSE
        False
    FI
ENDDEF

FUNCDEF isSyn ( t :: TCPPacket ) :: Bool ::=
    (flags(t) == CONS(SYN, NIL)) /\
    (payload(t) == "") /\
    (seq(t) == SEQ_VALID) /\
    (ack(t) == ACK_VALID) /\
    (sport(t) > 4002) /\ 
    (sport(t) < 5000)
ENDDEF

FUNCDEF isSynAck ( t :: TCPPacket ) :: Bool ::=
    (flags(t) == CONS(ACK, CONS(SYN, NIL))) /\
    (payload(t) == "") /\
    (seq(t) == SEQ_VALID) /\
    (ack(t) == ACK_VALID)
ENDDEF

FUNCDEF isAck ( t :: TCPPacket ) :: Bool ::=
    (flags(t) == CONS(ACK, NIL)) /\
    (seq(t) == SEQ_VALID) /\
    (ack(t) == ACK_VALID) /\
    (payload(t) == "")
ENDDEF

FUNCDEF isFinAck ( t :: TCPPacket ) :: Bool ::=
    (flags(t) == CONS(ACK, CONS(FIN, NIL))) /\
    (seq(t) == SEQ_VALID) /\
    (ack(t) == ACK_VALID) /\
    (payload(t) == "")
ENDDEF

FUNCDEF synack ( sport :: Int; dport :: Int ) :: TCPPacket ::=
    TCPPacket(sport, dport, SEQ_VALID, ACK_VALID, CONS(ACK, CONS(SYN, NIL)), "")
ENDDEF

FUNCDEF syn ( sport :: Int; dport :: Int ) :: TCPPacket ::=
    TCPPacket(sport, dport, SEQ_VALID, ACK_VALID, CONS(SYN, NIL), "")
ENDDEF

FUNCDEF ack ( sport :: Int; dport :: Int ) :: TCPPacket ::=
    TCPPacket(sport, dport, SEQ_VALID, ACK_VALID, CONS(ACK, NIL), "")
ENDDEF

FUNCDEF finack ( sport :: Int; dport :: Int ) :: TCPPacket ::=
    TCPPacket(sport, dport, SEQ_VALID, ACK_VALID, CONS(ACK, CONS(FIN, NIL)), "")
ENDDEF

PROCDEF parallelClose [ InSutNet :: TCPPacket; OutSutNet :: TCPPacket; InSutUser :: TCPUserCallInput; OutSutUser :: TCPUserCallOutput ] 
        (st_src_port, st_dst_port :: Int) ::=

	(OutSutNet ! finack(st_src_port, st_dst_port) >->
	    InSutNet ? a [[ (dport(a) == st_src_port) /\ (sport(a) == st_dst_port) /\ isAck(a) ]])
    |||
	(OutSutUser ? r [[ isSUCCESS(r) ]])

ENDDEF

-- ----------------------------------------------------------------------------------------- --

STAUTDEF sutPassiveOpenStaut [ InSutNet :: TCPPacket; OutSutNet :: TCPPacket; InSutUser :: TCPUserCallInput; OutSutUser :: TCPUserCallOutput ] ( )
   ::=
        STATE
            qclosed, qlisten, qsynrecv_1, qsynrecv_2, qestablished_1, qestablished_2,
			qestablished, qclosewait_1, qclosewait_2, qclosewait_3, qlastack_1, qlastack_2
        VAR
            st_src_port :: Int; st_dst_port :: Int
        INIT
            qclosed { st_src_port := 0; st_dst_port := 0 }
        TRANS
            qclosed ->  InSutUser ? c [[ isListenUserCall(c) ]] { st_src_port := lport(c) } ->  qlisten
            qlisten ->  InSutNet ? s [[ (dport(s) == st_src_port) /\ isSyn(s) ]] { st_dst_port := sport(s) } -> qsynrecv_1
            qsynrecv_1 -> OutSutNet ! synack(st_src_port, st_dst_port) -> qsynrecv_2
            qsynrecv_2 -> InSutNet ? a [[ isAck(a) /\ (dport(a) == st_src_port) /\ (sport(a) == st_dst_port) ]] -> qestablished_1
            qestablished_1 -> OutSutUser ? r [[ isSUCCESS(r) ]] -> qestablished_2

			qestablished_2 -> InSutNet ? f [[ (dport(f) == st_src_port) /\ (sport(f) == st_dst_port) /\ isFinAck(f) ]] -> qclosewait_1
			qclosewait_1 -> OutSutNet ! ack(st_src_port, st_dst_port) -> qclosewait_2
			qclosewait_2 -> InSutUser ? c [[ isCLOSE(c) ]] -> qclosewait_3
            qclosewait_3 -> parallelClose [ InSutNet, OutSutNet, InSutUser, OutSutUser ] (st_src_port, st_dst_port) -> qclosed
			--qclosewait_3 -> OutSutNet ! finack(st_src_port, st_dst_port) -> qlastack_1
			--qlastack_1 -> InSutNet ? a [[ (dport(a) == st_src_port) /\ (sport(a) == st_dst_port) /\ isAck(a) ]] -> qclosewait_2
			--qlastack_2 -> OutSutUser ? r [[ isSUCCESS(r) ]] -> qclosed
ENDDEF

-- ----------------------------------------------------------------------------------------- --

MODELDEF  Tcp
   ::=
        CHAN IN    InSutNet, InSutUser
        CHAN OUT   OutSutNet, OutSutUser

        BEHAVIOUR sutPassiveOpenStaut [ InSutNet, OutSutNet, InSutUser, OutSutUser ] ( )
ENDDEF


-- ----------------------------------------------------------------------------------------- --

CNECTDEF  Sut
   ::=
        CLIENTSOCK

        CHAN  OUT  InSutNet                     HOST "localhost"  PORT 2990
        ENCODE     InSutNet ? x                 ->  ! toString(x)

        CHAN  IN   OutSutNet                    HOST "localhost"  PORT 2990
        DECODE     OutSutNet ! fromString(s)    <-  ? s

        CHAN  OUT  InSutUser                    HOST "192.168.1.147"  PORT 3014
        ENCODE     InSutUser ? t                ->  ! toString(t)

        CHAN  IN   OutSutUser                   HOST "192.168.1.147"  PORT 3014
        DECODE     OutSutUser ! fromString(m)   <-  ? m
ENDDEF

-- ----------------------------------------------------------------------------------------- --
-- ----------------------------------------------------------------------------------------- --

